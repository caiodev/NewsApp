apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization'
apply plugin: 'jacoco'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "br.com.caiodev.newsapi"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        debug {
            buildConfigField 'String', "ApiKey", API_KEY
            resValue 'string', "api_key", API_KEY
            debuggable = true
            signingConfig signingConfigs.debug
            testCoverageEnabled = true
        }

        release {
            buildConfigField 'String', "ApiKey", API_KEY
            resValue 'string', "api_key", API_KEY
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    sourceSets {
        androidTest {
            java.srcDirs += "src/sharedTest/java"
        }
        test {
            java.srcDirs += "src/sharedTest/java"
        }
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //AndroidXCore
    implementation "androidx.core:core:$androidXCore"

    //AppCompat
    implementation "androidx.appcompat:appcompat:$appCompat"

    //Coil
    implementation("io.coil-kt:coil:$coil")

    //Constraint layout
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayout"

    //Koin
    //For Android
    implementation "org.koin:koin-android:$koin"
    //For Lifecycle scoping
    implementation "org.koin:koin-androidx-scope:$koin"
    //For Android Architecture ViewModel
    implementation "org.koin:koin-androidx-viewmodel:$koin"

    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin"

    //Ktx
    implementation "androidx.core:core-ktx:$ktxCore"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$arch"

    //LeakCanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanary"

    //Lifecycle section
    implementation "androidx.lifecycle:lifecycle-viewmodel:$arch"
    implementation "androidx.lifecycle:lifecycle-extensions:$arch"
    implementation "androidx.lifecycle:lifecycle-runtime:$arch"
    implementation "androidx.lifecycle:lifecycle-common-java8:$arch"

    //Logging section
    implementation "com.squareup.okhttp3:logging-interceptor:$loggingInterceptor"
    implementation "com.jakewharton.timber:timber:$timber"

    //Material design
    implementation "com.google.android.material:material:$materialDesign"

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:$serializationConverter"

    //Serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization"

    //*Testing section*/

    //*Test source set*/

    //Test core
    testImplementation "androidx.test:core-ktx:$testCore"

    //ArchTestCore
    testImplementation "androidx.arch.core:core-testing:$archTest"

    //Jacoco
    testImplementation "org.jacoco:org.jacoco.ant:$jacocoVersion"

    //JUnit
    testImplementation "org.junit.jupiter:junit-jupiter:$junit5"

    /*AndroidTest source set*/
    //Test core
    androidTestImplementation "androidx.test:core-ktx:$testCore"
    androidTestImplementation "androidx.test:runner:$runner"

    //JUnit
    androidTestImplementation "androidx.test.ext:junit:$junitAndroidX"

    //Espresso
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso"
    androidTestImplementation "androidx.test.espresso:espresso-accessibility:$espresso"
}

jacoco {
    toolVersion = "$jacocoVersion"
    reportsDir = file("$buildDir")
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {

    def coverageSourceDirs = [
            "src/main/java"
    ]

    def fileFilter = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*'
    ]

    def javaClasses = fileTree(dir: "$buildDir/intermediates/classes/debug", excludes: fileFilter)
    def kotlinClasses = fileTree(dir: "$buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)

    classDirectories.setFrom files([javaClasses, kotlinClasses])
    additionalSourceDirs.setFrom files(coverageSourceDirs)

    sourceDirectories.setFrom files([
            "$project.projectDir/src/main/java"
    ])

    executionData.setFrom fileTree(dir: project.projectDir, includes: ["**/*.exec"])

    reports {
        html.enabled true
    }
}